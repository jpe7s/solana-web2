ext {
  VERSION = project.findProperty('version')
  VCS_URL = 'https://github.com/sava-software/solana-web2'
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'

project.group = 'software.sava'
project.version = "$VERSION"

final JLV = JavaLanguageVersion.of(project.findProperty('javaVersion') as Integer ?: 23)

plugins.withType(JavaPlugin).configureEach {
  java {
    modularity.inferModulePath = true
    toolchain {
      languageVersion = JLV
    }
  }
}

dependencies {
  implementation libs.bouncycastle
  implementation libs.json.iterator
  implementation libs.sava.core
  implementation libs.sava.rpc

  testImplementation libs.junit.jupiter
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

repositories {
  maven {
    url = "https://maven.pkg.github.com/comodal/json-iterator"
    credentials {
      username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
      password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
    }
  }
  maven {
    url = "https://maven.pkg.github.com/sava-software/sava"
    credentials {
      username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
      password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
    }
  }
  mavenCentral()
}

test {
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
    exceptionFormat = "full"
    showStandardStreams = true
  }
}

tasks.register('sourcesJar', Jar) {
  from sourceSets.main.allJava
  archiveClassifier.set('sources')
}

tasks.withType(Javadoc).configureEach {
  options.addStringOption('Xdoclint:none', '-quiet')
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      groupId = project.group
      artifactId = project.name
      version = project.version

      pom {
        name = project.name
        description = "Solana Ecosystem HTTP Clients"
        url = "$VCS_URL"
        licenses {
          license {
            name = 'MIT License'
            url = 'https://github.com/sava-software/solana-web2/blob/main/LICENSE'
          }
        }
        developers {
          developer {
            name = 'Jim'
            id = 'jpe7s'
            email = 'jpe7s.salt188@passfwd.com'
            organization = 'Sava Software'
            organizationUrl = 'https://github.com/sava-software'
          }
        }
        scm {
          connection = 'scm:git:git@github.com:sava-software/solana-web2.git'
          developerConnection = 'scm:git:ssh@github.com:sava-software/solana-web2.git'
          url = "$VCS_URL"
        }
      }
    }
  }

  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/sava-software/solana-web2"
      credentials {
        username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user.write")
        password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token.write")
      }
    }
  }
}
